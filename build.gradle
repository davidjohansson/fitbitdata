ext {
    springBootVersion = '1.1.8.RELEASE'
}

apply plugin: 'java'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

buildscript {
    repositories {
        maven {
            url 'http://maven.springframework.org/release'
        }
        maven {
            url 'http://maven.springframework.org/milestone'
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.6.RELEASE")
    }
}


dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile 'io.reactivex:rxjava:1.0.0'
    testCompile 'junit:junit:4.10'
}


task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into project.rootDir 
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)

clean << {
    project.file('app.jar').delete()
}

task wrapper(type: Wrapper) {
    description = "Generates gradlew scripts"
    gradleVersion = "2.1"

    doLast() {
        def gradleOpts = "-Xmx512m -Dfile.encoding=utf8"
        File wrapperFile = file("gradlew")
        wrapperFile.text = wrapperFile.text.replace("DEFAULT_JVM_OPTS=",
                "GRADLE_OPTS=\"$gradleOpts \$GRADLE_OPTS\"\nDEFAULT_JVM_OPTS=")
    }
}
